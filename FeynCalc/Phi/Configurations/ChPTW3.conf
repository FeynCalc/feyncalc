(* ************************************************************** *)
(*                                                                *)
(*                      ChPTW3                                    *)
(*                                                                *)
(* ************************************************************** *)

(*
   Author:              F.Orellana

   Year:                2000

   Mathematica Version: 3.0

   Requirements:        FeynCalc > 3, PHI

   Summary:             Configuration file for the family of
                        Lagrangians ChPTW3 for PHI

   Description:         Definitions for standard weak ChPT lagrangians
                        The definitions follow
                        Ecker, Kambor and Wyler (1992), CERN-TH.6610/92.

*)

(* ************************************************************** *)

(* Modified box definitions for FeynCalc *)

<<"FCBoxes.m"

(* ************************************************************** *)

(* SU(3) meson matrix PhiMeson instead of Pion *)

BeginPackage["HighEnergyPhysics`Phi`Objects`", {"HighEnergyPhysics`Phi`",
"HighEnergyPhysics`FeynCalc`"}];
Begin["`Private`"];

MM[x_?(! NumberQ[#] &), h___, opts___Rule] /; (fcexpt/.Flatten[{opts}]/.Options[MM]) :=

    UFieldMatrix[fcqf[(Particle[PhiMeson, h])][x], ##] & @@
      Union[OptionsSelect[UFieldMatrix, opts], OptionsSelect[UMatrix, opts]];
MM[ii_?NumberQ, x_, h___, opts___Rule] /; (fcexpt/.Flatten[{opts}]/.Options[MM]) :=
    UFieldMatrix[ii, fcqf[(Particle[PhiMeson, h])][x], ##] & @@
      Union[OptionsSelect[UFieldMatrix, opts], OptionsSelect[UMatrix, opts]];
SMM[x_, h___, opts___Rule] /; (fcexpt/.Flatten[{opts}]/.Options[SMM]) := MM[1/2, x, h, opts];
MMS[x_, h___, opts___Rule] /; (fcexpt/.Flatten[{opts}]/.Options[MMS]) :=
    UFieldMatrixSeries[fcqf[(Particle[PhiMeson, h])][x], ##] & @@
       Union[OptionsSelect[UFieldMatrix, opts], OptionsSelect[UMatrix, opts]];

End[];
EndPackage[];

(* ************************************************************** *)

(* Usage definitions *)

HighEnergyPhysics`Phi`Objects`ChPTW3::"usage"=
"Lagrangian[ChPTW3[2]] is the weak mesonic Chiral Perturbation Theory
lagrangian written in terms of a matrix representation
of the meson fields to second order in the energy.  Lagrangian[ChPTW3[4]]
is the weak mesonic Chiral Perturbation Theory lagrangian
to four'th order in the energy.  To evaluate use ArgumentsSupply";

HighEnergyPhysics`Phi`Objects`ChPT3;

MM::"usage"="MM[x] :=
UFieldMatrix[QuantumField[Particle[PhiMeson]][x]].
MM takes two optional arguments with head RenormalizationState
and RenormalizationScheme respectively.";

HighEnergyPhysics`Phi`Objects`UDelta::"usage"=
"UDelta is the delta-field of WChPT
(Ecker, Kambor and Wyler (1992), CERN-TH.6610/92).
To evaluate use ArgumentsSupply.";

HighEnergyPhysics`Phi`Objects`UNablaHatDelta::"usage"=
"UNablaHatDelta[mu] is the nabla hat of the
delta-field of WChPT
(Ecker, Kambor and Wyler (1992), CERN-TH.6610/92).
To evaluate use ArgumentsSupply.";

HighEnergyPhysics`Phi`Objects`UOmega::"usage"=
"UOmega[mu,nu] is the omega-field of WChPT
(Ecker, Kambor and Wyler (1992), CERN-TH.6610/92).
To evaluate use ArgumentsSupply.";

HighEnergyPhysics`Phi`Objects`CovariantNabla::"usage"=
"CovariantNabla[f[x],x,{li1,li2,...},opts] is
the covariant derivative of a h h^(-1)  transforming quantity  f[x] with respect to
space-time variables x and with Lorentz indices li0, li1, li2,...
(See e.g ChPT (Ecker, Kambor and Wyler (1992), CERN-TH.6610/92)).
To evaluate use ArgumentsSupply.";

HighEnergyPhysics`Phi`Objects`NablaChiPlus::"usage"=
"NablaChiPlus is CovariantNabla acting on UChiPlus.";

HighEnergyPhysics`Phi`Objects`NablaChiMinus::"usage"=
"NablaChiPlus is CovariantNabla acting on UChiMinus.";

HighEnergyPhysics`Phi`Objects`C8::"usage"=
"C8 is the coefficient C_8 of Kambor, Missimer and Wyler, \
Nucl.Phys.B346:17-64,1990.";

HighEnergyPhysics`Phi`Objects`C8Dot::"usage"=
"C8Dot is the coefficient C_8' of Kambor, Missimer and Wyler, \
Nucl.Phys.B346:17-64,1990.";

HighEnergyPhysics`Phi`Objects`C8::"usage"=
"C27 is the coefficient C_27 of Kambor, Missimer and Wyler, \
Nucl.Phys.B346:17-64,1990.";

HighEnergyPhysics`Phi`Objects`FW::"usage"=
"FW[a,b] is the coefficient f^a_b of Kambor, Missimer and Wyler, \
Nucl.Phys.B346:17-64,1990.";

HighEnergyPhysics`Phi`Objects`QW::"usage"=
"QW[a,b] is an SU(3) generator matrix Q_a^b in the Cartan-Weyl basis, \
as used in Kambor, Missimer and Wyler, \
Nucl.Phys.B346:17-64,1990.";

HighEnergyPhysics`Phi`Objects`KW::"usage"=
"KW[a,b] is the K^a_b of Kambor, Missimer and Wyler, \
Nucl.Phys.B346:17-64,1990.";

HighEnergyPhysics`Phi`Objects`TW::"usage"=
"TW[a,b,c,d] is the t^{a b}_{c d} of Kambor, Missimer and Wyler, \
Nucl.Phys.B346:17-64,1990.";

(* ************************************************************** *)

(* Box definitions *)

UOmega /: MakeBoxes[UOmega[mu_,nu_], TraditionalForm] :=
    SuperscriptBox[
      MakeBoxes[
          StyleForm["\[Omega]", FontSlant -> "Italic",
          FontWeight -> "Bold"]][[1]],
      RowBox[{MakeBoxes[TraditionalForm[mu]],
              MakeBoxes[TraditionalForm[nu]]}]];

FW /: MakeBoxes[FW[a_,b_], TraditionalForm] :=
    SubsuperscriptBox[
      "f", MakeBoxes[TraditionalForm[b]],
              MakeBoxes[TraditionalForm[a]]];

QW /: MakeBoxes[QW[a_,b_], TraditionalForm] :=
    SubsuperscriptBox[
      MakeBoxes[
          StyleForm["Q", (*FontSlant -> "Italic",*)
          FontWeight -> "Bold"]],
              MakeBoxes[TraditionalForm[b]],
              MakeBoxes[TraditionalForm[a]]];

KW /: MakeBoxes[KW[a_,b_][x_], TraditionalForm] :=
    SubsuperscriptBox[
      MakeBoxes[
          StyleForm["K", (*FontSlant -> "Italic",*)
          FontWeight -> "Bold"]],
              MakeBoxes[TraditionalForm[b]],
              MakeBoxes[TraditionalForm[a]]];

KW /: MakeBoxes[KW[a_,b_], TraditionalForm] :=
    SubsuperscriptBox[
      MakeBoxes[
          StyleForm["K", (*FontSlant -> "Italic",*)
          FontWeight -> "Bold"]],
              MakeBoxes[TraditionalForm[b]],
              MakeBoxes[TraditionalForm[a]]];

TW /: MakeBoxes[TW[a_,b_,c_,d_], TraditionalForm] :=
    SubsuperscriptBox[
      "t", RowBox[{MakeBoxes[TraditionalForm[c]],
                   MakeBoxes[TraditionalForm[d]]}],
           RowBox[{MakeBoxes[TraditionalForm[a]],
                   MakeBoxes[TraditionalForm[b]]}]];

UDelta /: MakeBoxes[UDelta, TraditionalForm] :=
     MakeBoxes[
          StyleForm["\[CapitalDelta]",
          FontSlant -> "Italic",FontWeight->"Bold"]];

C8 /: MakeBoxes[C8, TraditionalForm] :=
     SubscriptBox["C","8"];

C8Dot /: MakeBoxes[C8Dot, TraditionalForm] :=
     SubsuperscriptBox["C","8","\[Prime]"];

C27 /: MakeBoxes[C27, TraditionalForm] :=
     SubscriptBox["C","27"];

UNablaHatDelta /: MakeBoxes[UNablaHatDelta[mu_], TraditionalForm] :=
     RowBox[{SubscriptBox[MakeBoxes[OverHat["\[Del]"]],
	  MakeBoxes[TraditionalForm[mu]]],
          MakeBoxes[
          StyleForm["\[CapitalDelta]",
          FontSlant -> "Italic",FontWeight->"Bold"]]}];

NablaChiPlus /: MakeBoxes[NablaChiPlus[mu_], TraditionalForm] :=
     RowBox[{SubscriptBox[MakeBoxes[OverHat["\[Del]"]],
	  MakeBoxes[TraditionalForm[mu]]],
          SubscriptBox[MakeBoxes["\[Chi]"],
          MakeBoxes["+"]]}];

(* ************************************************************** *)

(* Source fields *)

(* Strip off options passed to Particle[PseudoScalar[0],___]*)
Particle[PseudoScalar[0],o___,__Rule]:=Particle[PseudoScalar[0],o];

(* The scalar source is set to the quark mass matrix *)

QuantumField[Particle[Scalar[0],st___RenormalizationState,
sc___RenormalizationScheme,qs___ExpansionState,
opts___Rule|opts___List],SUNIndex[0]][x_]:=
UQuarkMassMatrix[st,sc,qs,opts];

(* ************************************************************** *)

(* Adjoints and conjugates *)

(* Adjoints and conjugates *)

Adjoint[p:QuantumField[___,Particle[
(Vector|AxialVector|Scalar|PseudoScalar)[0|1|2|12],___],___][_]] = p;

Adjoint[p:QuantumField[___,Particle[
(Vector|AxialVector|Scalar|PseudoScalar)[0|1|2|12],___],___]] = p;

Unprotect[Conjugate];

Conjugate[p:QuantumField[___,Particle[
(Vector|AxialVector|Scalar|PseudoScalar)[0|1|2|12],___],___][_]] = p;

Conjugate[p:QuantumField[___,Particle[
(Vector|AxialVector|Scalar|PseudoScalar)[0|1|2|12],___],___]] = p;

Protect[Conjugate];

(* ************************************************************** *)

(* Antiparticles *)

ChargeConjugate[Pion] = Pion;
ChargeConjugate[Vector[0]] = Vector[0];
ChargeConjugate[AxialVector[0]] = AxialVector[0];
ChargeConjugate[Scalar[0]] = Scalar[0];
ChargeConjugate[Scalar[1]] = Scalar[1];
ChargeConjugate[PseudoScalar[0]] = PseudoScalar[0];
ChargeConjugate[PseudoScalar[1]] = PseudoScalar[1];
ChargeConjugate[PionPlus] = PionMinus;
ChargeConjugate[PionMinus] = PionPlus;
ChargeConjugate[PionZero] = PionZero;
ChargeConjugate[KaonMinus] = KaonPlus;
ChargeConjugate[KaonPlus] = KaonMinus;
ChargeConjugate[KaonZero] = KaonZeroBar;
ChargeConjugate[KaonZeroBar] = KaonZero;
ChargeConjugate[EtaMeson] = EtaMeson;

(* ************************************************************** *)

(* Constants *)

(* When calculating infrared divergences, the photon mass
   should not be set to zero *)

ParticleMass[Photon,___]:=0;
ParticleMass[Photon[0],___]:=0;

(*Strong part*)
(*CouplingConstant[ChPT3[4],1,___]=0;
CouplingConstant[ChPT3[4],2,___]=0;
CouplingConstant[ChPT3[4],3,___]=0;
CouplingConstant[ChPT3[4],4,___]=0;
CouplingConstant[ChPT3[4],5,___]=0;
CouplingConstant[ChPT3[4],6,___]=0;
CouplingConstant[ChPT3[4],7,___]=0;
CouplingConstant[ChPT3[4],8,___]=0;
CouplingConstant[ChPT3[4],9,___]=0;
CouplingConstant[ChPT3[4],10,___]=0;
CouplingConstant[ChPT3[4],11,___]=0*)

(*Weak part*)
(*CouplingConstant[ChPTW3[4],1,___]=0;
CouplingConstant[ChPTW3[4],2,___]=0;
CouplingConstant[ChPTW3[4],3,___]=0;
CouplingConstant[ChPTW3[4],4,___]=0;*)
(*CouplingConstant[ChPTW3[4],5,___]=0;
CouplingConstant[ChPTW3[4],6,___]=0;
CouplingConstant[ChPTW3[4],7,___]=0;
CouplingConstant[ChPTW3[4],8,___]=0;
CouplingConstant[ChPTW3[4],9,___]=0;
CouplingConstant[ChPTW3[4],10,___]=0;
CouplingConstant[ChPTW3[4],11,___]=0;
CouplingConstant[ChPTW3[4],12,___]=0;
CouplingConstant[ChPTW3[4],13,___]=0;*)
(*CouplingConstant[ChPTW3[4],14,___]=0;
CouplingConstant[ChPTW3[4],15,___]=0;
CouplingConstant[ChPTW3[4],16,___]=0;
CouplingConstant[ChPTW3[4],17,___]=0;*)
(*CouplingConstant[ChPTW3[4],18,___]=0;*)
(*CouplingConstant[ChPTW3[4],19,___]=0;
CouplingConstant[ChPTW3[4],20,___]=0;
CouplingConstant[ChPTW3[4],21,___]=0;
CouplingConstant[ChPTW3[4],22,___]=0;
CouplingConstant[ChPTW3[4],23,___]=0;
CouplingConstant[ChPTW3[4],24,___]=0;*)
(*CouplingConstant[ChPTW3[4],25,___]=0;
CouplingConstant[ChPTW3[4],26,___]=0;
CouplingConstant[ChPTW3[4],27,___]=0;
CouplingConstant[ChPTW3[4],28,___]=0;
CouplingConstant[ChPTW3[4],29,___]=0;
CouplingConstant[ChPTW3[4],30,___]=0;
CouplingConstant[ChPTW3[4],31,___]=0;
CouplingConstant[ChPTW3[4],32,___]=0;
CouplingConstant[ChPTW3[4],33,___]=0;
CouplingConstant[ChPTW3[4],34,___]=0;
CouplingConstant[ChPTW3[4],35,___]=0;
CouplingConstant[ChPTW3[4],36,___]=0;
CouplingConstant[ChPTW3[4],37,___]=0;*)

(* ************************************************************** *)

(* Redefinition of the left and right components.
   Ecker, Kambor and Wyler have them the other way round.
   Also it's necessary in order to not have the AxialVector (Vector)
   part set to zero *)

IsoVector[QuantumField[aa___, Particle[LeftComponent[a_,opts0___Rule],i___],
bb___],opts___][x_]/;(Explicit/.{opts0}/.Options[LeftComponent]):=
1/2*(
IsoVector[QuantumField[aa, Particle[Vector[a],i],bb],opts][x]-
IsoVector[QuantumField[aa, Particle[AxialVector[a],i],bb],opts][x]);

IsoVector[QuantumField[aa___, Particle[RightComponent[a_,opts0___Rule],i___],bb___],
opts___][x_]/;(Explicit/.{opts0}/.Options[RightComponent]):=1/2*(
  IsoVector[QuantumField[aa, Particle[Vector[a],i],bb],opts][x]+
  IsoVector[QuantumField[aa, Particle[AxialVector[a],i],bb],opts][x]);

QuantumField[aa___, Particle[LeftComponent[a_,opts0___Rule],i___],bb___][x_]/;
(Explicit/.{opts0} /. Options[LeftComponent]):=
  1/2*(QuantumField[aa, Particle[Vector[a],i],bb][x]-
        QuantumField[aa, Particle[AxialVector[a],i],bb][x]);

QuantumField[aa___, Particle[RightComponent[a_,opts0___Rule],i___],bb___][x_]/;
(Explicit/.{opts0} /. Options[RightComponent]):=
  1/2*(QuantumField[aa, Particle[Vector[a],i],bb][x]+
        QuantumField[aa, Particle[AxialVector[a],i],bb][x]);

(* ************************************************************** *)

(* Redefinition of the field strength tensor: -I instead of I *)

FST[p_,mu_LorentzIndex,
    nu_LorentzIndex,x_,
    a___RenormalizationState,
    b___RenormalizationScheme,(opts___Rule|opts___List)]:=
  FieldStrengthTensor[mu,
UGeneratorMatrixIsoDotFull[QuantumField[
Particle[p,a,b],nu][x],opts],x]-2*I*
      UGeneratorMatrixIsoDot[
        IsoCross[(
              IsoVector[QuantumField[Particle[p,a,b],mu],##]&@@
                OptionsSelect[IsoVector,opts])[
            x],(IsoVector[QuantumField[Particle[p,a,b],nu],##]&@@
                OptionsSelect[IsoVector,opts])[x]],opts];

(* ************************************************************** *)

(* The capital delta field *)

$Substitutions = Append[$Substitutions, UDelta :>
NM[SMM,
UGeneratorMatrix[SUNIndex[6]],
Adjoint[SMM]]];

$Substitutions = Append[$Substitutions, UDelta[x_] :>
NM[SMM[x],
UGeneratorMatrix[SUNIndex[6]],
Adjoint[SMM[x]]]];

(* ************************************************************** *)

(* The K field of Kambor, Missimer and Wyler *)

$Substitutions = Append[$Substitutions, KW[a_, b_][x_] :>
NM[SMM[x], QW[a, b], Adjoint[SMM[x]]]];

$Substitutions = Append[$Substitutions, KW[a_, b_] :>
NM[SMM, QW[a, b], Adjoint[SMM]]];

$Substitutions = Append[$Substitutions, KW :>
NM[SMM, QW, Adjoint[SMM]]];

(* ************************************************************** *)

(* The nabla hat of the capital delta field *)

(* Uncomment one and comment the other of the definitions below;
   the first is without and the second with
   coupling to a scalar source *)

$Substitutions = Append[$Substitutions, UNablaHatDelta[mu_] :>

(* The standard definition *)
-I*NM[SMM,
UGeneratorMatrixIsoDot[QuantumField[Particle[
LeftComponent[0]],{mu}]],
UGeneratorMatrix[SUNIndex[6]],
Adjoint[SMM]]+
I*NM[SMM,
UGeneratorMatrix[SUNIndex[6]],
UGeneratorMatrixIsoDot[QuantumField[Particle[
LeftComponent[0]],{mu}]],
Adjoint[SMM]]

(* Including a scalar 'source' with momentum. Forget about the 
   vector and axial-vector sources *)
(*NM[SMM,NM[
FDr[QuantumField[Particle[Scalar[1]]],{mu}],
UGeneratorMatrix[SUNIndex[6]],
Adjoint[SMM]]]*)
];

(* ************************************************************** *)

(* The omega field *)

$Substitutions = Append[$Substitutions, UOmega[mu_,nu_] :>

CovariantNabla[USmall[mu],{nu}]+CovariantNabla[USmall[nu],{mu}]];

(* ************************************************************** *)

(* The Gamma_mu field *)

$Substitutions = Append[$Substitutions,
UGamma[mu_,ar___RenormalizationState,br___RenormalizationScheme,
cr___ExpansionState,(opts___Rule|opts___List)][x_] :>

1/2*(

NM[Adjoint[SMM[x,ar,br,cr,opts]],
FDr[SMM[x,ar,br,cr,opts],x,{mu}]]-

I*NM[Adjoint[SMM[x,ar,br,cr,opts]],
UGeneratorMatrixIsoDot[QuantumField[Particle[
RightComponent[0],ar,br,cr],{mu}][x]],
SMM[x,ar,br,cr,opts]]+

NM[SMM[x,ar,br,cr,opts],
FDr[Adjoint[SMM[x,ar,br,cr,opts]],x,{mu}]]-

I*NM[SMM[x,ar,br,cr,opts],
UGeneratorMatrixIsoDot[QuantumField[Particle[
LeftComponent[0],ar,br,cr],{mu}][x]],
Adjoint[SMM[x,ar,br,cr,opts]]]

)];

$Substitutions = Append[$Substitutions, UGamma[mu_] :>

1/2*(

NM[Adjoint[SMM],FDr[SMM,{mu}]]-

I*NM[Adjoint[SMM],
UGeneratorMatrixIsoDot[QuantumField[Particle[
RightComponent[0]],{mu}]],SMM]+

NM[SMM,FDr[Adjoint[SMM],{mu}]]-

I*NM[SMM,
UGeneratorMatrixIsoDot[QuantumField[Particle[
LeftComponent[0]],{mu}]],Adjoint[SMM]]

)];

(* ************************************************************** *)

(* The nabla of the u_mu field *)

Options[CovariantNabla] = {Explicit -> True};

CovariantNabla[aa_,x_,loris__LorentzIndex,lori1_LorentzIndex, opts___Rule]:=
(newfuncc[1]=CovariantNabla[aa,x,lori1, opts];
Do[newfuncc[rep+1]=CovariantNabla[
newfuncc[rep],x,##,opts]&@@Take[{loris},{-rep}],
{rep,1,Length[{loris}]}];newfuncc[Length[{loris}]+1]);

CovariantNabla[aa_,x_,{loris__},rest___] :=
CovariantNabla[aa,x,##,rest]& @@ LorentzIndex /@ {loris};

(* -------------------------------------------------------------- *)

CovariantNabla[aa_,{lori_},(opts___Rule|opts___List)] /;
(Explicit /. Flatten[{opts}] /. Options[CovariantNabla]) :=

FieldDerivative[aa,{lori}] +

NM[UGamma[lori],aa] -
NM[aa,UGamma[lori]];

CovariantNabla[aa_,x_,
lori_LorentzIndex,
ar___RenormalizationState,br___RenormalizationScheme,
cr___ExpansionState,(opts___Rule|opts___List)] /;
(Explicit /. Flatten[{opts}] /. Options[CovariantNabla]) :=

FieldDerivative[aa,x,lori] +

NM[UGamma[lori,ar,br,cr,opts][x],aa] -
NM[aa,UGamma[lori,ar,br,cr,opts][x]];


(* ************************************************************** *)

(* The nabla of the Chi_plus/Chi_minus fields *)

$Substitutions = Append[$Substitutions, NablaChiPlus[mu_] :>
NM[SMM,CDr[Adjoint[UChiMatrix],{mu}],SMM]+
NM[Adjoint[SMM],CDr[UChiMatrix,{mu}],Adjoint[SMM]]+
I/2*(NM[USmall[mu],UChiMinus]+NM[UChiMinus,USmall[mu]])];

(* Not used *)
$Substitutions = Append[$Substitutions, NablaChiMinus[mu_] :>
NM[SMM,CDr[Adjoint[UChiMatrix],{mu}],SMM]-
NM[Adjoint[SMM],CDr[UChiMatrix,{mu}],Adjoint[SMM]]+
I/2*(NM[USmall[mu],UChiPlus]+NM[UChiPlus,USmall[mu]])];

(* ************************************************************** *)

(* The covariant derivative *)

CovariantFieldDerivative[aa_,x_,{loris__},rest___] :=
CovariantFieldDerivative[aa,x,##,rest]& @@ LorentzIndex /@ {loris};

(* -------------------------------------------------------------- *)

(* Standard ChPT - Donoghue, Golowich, Holstein *)
(* commuted 14/1-2001*)

CovariantFieldDerivative[aa_,x_,
lori_LorentzIndex,
ar___RenormalizationState,br___RenormalizationScheme,
cr___ExpansionState,(opts___Rule|opts___List)] /;
(Explicit /. Flatten[{opts}] /. Options[CovariantFieldDerivative]) :=

FieldDerivative[aa,x,lori] +

I*NM[aa,UGeneratorMatrixIsoDot[QuantumField[Particle[
LeftComponent[0],ar,br],lori][x],opts]] -

I*NM[UGeneratorMatrixIsoDot[QuantumField[Particle[
RightComponent[0],ar,br],lori][x],opts],aa];

(* ************************************************************** *)

(* Re-setting of options and $-variables *)

SetOptions[WriteOutUMatrices,SUNN->3];
SetOptions[UQuarkMass,SUNN->3,DiagonalToU->True];
SetOptions[UGenerator,SUNN->3];
SetOptions[WriteOutIsoVectors,SUNN->3];
SetOptions[UIdentity,SUNN->3];
SetOptions[UMatrix,SUNN->3];
SetOptions[IsoVector,SUNN->3];
SetOptions[UTrace,SUNN->3];
SetOptions[SUNReduce,SUNN->3];
SetOptions[ExpandU,SUNN->3];
SetOptions[FAToFC,EqualMasses->False];
SetOptions[DiscardTerms,
Retain->{Particle[PhiMeson,RenormalizationState[0]]->2,
Particle[AxialVector[0],RenormalizationState[0]]->1}];
SetOptions[CovariantFieldDerivative,SUNN->3];
SetOptions[DiscardTopologies,PerturbationOrder -> 1,
OrderingPatterns -> {(*Vertices with a scalar are always weak*)
FullVertex[___,Scalar1[0], ___],(*so are odd vertices without a scalar*)
FullVertex[v__] /; (OddQ[Length[{v}]] && FreeQ[{v}, Scalar1[0]])}];
SetOptions[UPerturb, SUNN -> 3];
SetOptions[CayleyHamiltonRules, UDimension -> 2];
SetOptions[CayleyHamiltonTrick, UDimension -> 3, 
    UMatrices :> {{I NM[
              Adjoint[CovariantFieldDerivative[MM[Global`x_], Global`x_, {Global`\[Rho]1_}]], 
              MM[Global`x_]], 
          I NM[Adjoint[MM[Global`x_]], 
              CovariantFieldDerivative[MM[Global`x_], Global`x_, {Global`\[Rho]2_}]], 
          I NM[Adjoint[CovariantFieldDerivative[MM[Global`x_], Global`x_, {Global`\[Rho]1_}]], 
              MM[Global`x_]], 
          I NM[Adjoint[MM[Global`x_]], 
              CovariantFieldDerivative[MM[Global`x_], Global`x_, {Global`\[Rho]2_}]]}}];
SetOptions[UReduce] = {UDimension -> 3};

DeclareUMatrix[MM,SMM,UChiMatrix,USmall,UFPlus,UFMinus,UChiPlus,UChiMinus,UGamma,QW,KW];

(* Auxiliary functions *)

SetOptions[UPerturb,
UFields -> {USmall, UChiPlus, UChiMinus, UFPlus, UFMinus, MM, KW}];

xi[x_] :=
  IsoDot[IsoVector[QuantumField[Particle[PseudoScalar[12]]]][x],
    IsoVector[UMatrix[UGenerator[]]]];

(*See Ecker 1992, CERN-TH-6660/92*)

uExpRight[x_,a___RenormalizationState,b___RenormalizationScheme,
      c___ExpansionState,opts___Rule]:=
    NM[SMM[x,Sequence@@OptionsSelect[SMM,opts]],
      UFieldMatrix[
        DecayConstant[UPerturbation,a,b,c]/DecayConstant[PhiMeson,a,b,c]/
          Sqrt[2],QuantumField[Particle[UPerturbation,a,b,c]][x],
        Sequence@@OptionsSelect[UFieldMatrix,opts]]];

uExpLeftAdj[x_,a___RenormalizationState,b___RenormalizationScheme,
      c___ExpansionState,opts___Rule]:=
    NM[UFieldMatrix[
        DecayConstant[UPerturbation,a,b,c]/DecayConstant[PhiMeson,a,b,c]/
          Sqrt[2],QuantumField[Particle[UPerturbation,a,b,c]][x],
        Sequence@@OptionsSelect[UFieldMatrix,opts]],
      SMM[x,Sequence@@OptionsSelect[SMM,opts]]];

(* The K field of Kambor, Missimer and Wyler *)

UCoefficient[KW][0][a_,b_,x_]=KW[a,b][x]

UCoefficient[KW][1][a_,b_,x_]=
  I/Sqrt[2]/DecayConstant[PhiMeson]UCommutator[xi[x],KW[a,b][x]]

UCoefficient[KW][2][a_,b_,
    x_]=-1/4/DecayConstant[PhiMeson]^2UCommutator[xi[x],
      UCommutator[xi[x],KW[a,b][x]]]

UCoefficient[KW][do_?((#>2)&)][a_,b_,x_]:=
    UCoefficient[KW][do][a,b,x]=(Message[UPerturb::"nocoeff",do];
        DiscardTerms[
            NM[uExpLeftAdj[x,ExpansionOrder -> do],QW[a,b],
                  Adjoint[uExpLeftAdj[x,ExpansionOrder -> do]]]//NMExpand//
              Expand,Retain -> {Particle[UPerturbation] -> do}]//
          UReduce);

$ParticlesInUse=
{PhiMeson,Pion,Kaon,KaonPlus,KaonMinus,
KaonZero,KaonZeroBar,(*EtaMeson,
Photon,Vector[0],*)AxialVector[0],Scalar[1],Scalar[2],
PseudoScalar[0]};

$ExpansionQuantities=
{FourVector[__],ParticleMass[Pion,a___],
ParticleMass[PhiMeson,b___],ParticleMass[KaonZero,c___],
ParticleMass[KaonPlus,d___],ParticleMass[EtaMeson,d___](*,
CouplingConstant[ChPTW3[2],h___]*)};

$PropagatorMassesStates={
    PhiMeson[0]->{RenormalizationState[0]},
    Pion[0]->{RenormalizationState[0]},
    Kaon[0]->{RenormalizationState[0]},
    KaonPlus[0]->{RenormalizationState[0]},
    KaonMinus[0]->{RenormalizationState[0]},
    KaonZero[0]->{RenormalizationState[0]},
    KaonZeroBar[0]->{RenormalizationState[0]},
    EtaMeson[0]->{RenormalizationState[0]},
    AxialVector[0][0]->{RenormalizationState[0]},
    PseudoScalar[0][0]->{RenormalizationState[0]},
    Vector[0]->{RenormalizationState[0]},
    Photon[0]->{RenormalizationState[0]}};

$CouplingIsoIndicesSpecifications =
   {PhiMeson[0]->{{IsoRange->{1,2,3,4,5,6,7,8},IsoIndicesString->"I"}},
    PseudoScalar[0][0]->{{IsoRange->{1,2,3,4,5,6,7,8},IsoIndicesString->"I"}},
    Scalar[2][0]->{{IsoRange->{0,1,2,3,4,5,6,7,8},IsoIndicesString->"I"}},
    AxialVector[0][0]->{{IsoRange->{1,2,3,4,5,6,7,8},IsoIndicesString->"I"}},
    Vector[0][0]->{{IsoRange->{1,2,3,4,5,6,7,8},IsoIndicesString->"I"}}};

$MixingFields=
   {};

$InsertOnly=
   {Scalar[1][0]->{
   HighEnergyPhysics`FeynArts`Incoming,
   HighEnergyPhysics`FeynArts`Outgoing,
   HighEnergyPhysics`FeynArts`External},
   Scalar[2][0]->{
   HighEnergyPhysics`FeynArts`Incoming,
   HighEnergyPhysics`FeynArts`Outgoing,
   HighEnergyPhysics`FeynArts`External},
   PseudoScalar[0][0]->{
   HighEnergyPhysics`FeynArts`Incoming,
   HighEnergyPhysics`FeynArts`Outgoing,
   HighEnergyPhysics`FeynArts`External},
   Vector[0][0]->{
   HighEnergyPhysics`FeynArts`Incoming,
   HighEnergyPhysics`FeynArts`Outgoing,
   HighEnergyPhysics`FeynArts`External},
   AxialVector[0][0]->{
   HighEnergyPhysics`FeynArts`Incoming,
   HighEnergyPhysics`FeynArts`Outgoing,
   HighEnergyPhysics`FeynArts`External},
   Photon[0]->{
   HighEnergyPhysics`FeynArts`Incoming,
   HighEnergyPhysics`FeynArts`Outgoing,
   HighEnergyPhysics`FeynArts`External}};

(* ************************************************************** *)
